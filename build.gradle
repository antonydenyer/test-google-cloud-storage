import org.jlleitschuh.gradle.ktlint.reporter.ReporterType

plugins {
    id 'idea'
    id 'org.jlleitschuh.gradle.ktlint' version '9.2.1'
    id 'org.jetbrains.kotlin.jvm' version '1.3.72'
    id 'org.unbroken-dome.test-sets' version '3.0.1'
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
        freeCompilerArgs = ['-Xjsr305=strict']
    }
}

repositories {
    mavenCentral()
    jcenter()
}

ktlint {
    verbose.set(true)
    outputToConsole.set(true)
    coloredOutput.set(true)
    reporters {
        reporter(ReporterType.CHECKSTYLE)
        reporter(ReporterType.JSON)
    }

    filter {
        exclude('**/style-violations.kt')
    }
}

test {
    useJUnitPlatform()
    testLogging {
        events('passed', 'skipped', 'failed')
    }
}

testSets { integrationTest { dirName = 'integration-test' } }

integrationTest {
    useJUnitPlatform()
    testLogging {
        events('passed', 'skipped', 'failed')
    }
}

check.dependsOn integrationTest
check.dependsOn ktlintCheck

dependencies {
    implementation('org.jetbrains.kotlin:kotlin-stdlib-jdk8')
    implementation('org.jetbrains.kotlin:kotlin-reflect')
    implementation("com.google.cloud:google-cloud-storage:1.111.2")

    testImplementation('org.junit.jupiter:junit-jupiter:5.6.2')
    testImplementation('io.mockk:mockk:1.10.0')
    testImplementation('com.natpryce:hamkrest:1.7.0.3')

    testImplementation("org.testcontainers:testcontainers:1.14.3")
    testImplementation("org.testcontainers:junit-jupiter:1.14.3")

}
